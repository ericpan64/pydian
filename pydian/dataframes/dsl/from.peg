# === Select DSL - 2. From expression ===

#  NOTE: Assume all whitespace is removed
from_expr = (union_expr / join_expr / subquery / table_alias) ~"$"

# === Actionable Units ===
join_expr = (table_alias / empty_alias) join_part+
join_part = join_op table_alias on_expr?
union_expr = table_alias (pplus table_alias)+
subquery = lparen select_expr rparen

# === Intermediate Representation ===
select_expr = ~"[^)]*"  # Match anything except closing parenthesis
join_op = (larrow / diamond)
on_expr = ~"(?i)on" lbrack name_list rbrack
name_list = name (comma name)*

# === Primitives ===
lbrack = "["
rbrack = "]"
lparen = "("
rparen = ")"
comma = ","
pplus = "++"
larrow = "<-"
diamond = "<>"

# === Lexemes ===
name = ~"[a-zA-Z_][a-zA-Z0-9_]*"  # Allow alphanumeric and underscore
table_alias = ~"[A-Z]"  # Single uppercase letter
empty_alias = ~""  # Empty string
